buildscript {
    ext {
        kotlin_version = "1.8.21"
        hilt_version = '2.44'
        compose_ui_version = '1.4.3'
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// Load file "keystore.properties" where we keep our keys
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (IOException ignored) {
    if (project.hasProperty('ossrhUsername')) keystoreProperties['ossrhUsername'] = ossrhUsername
    if (project.hasProperty('ossrhPassword')) keystoreProperties['ossrhPassword'] = ossrhPassword
}

allprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        if (keystoreProperties.containsKey("MAPBOX_DOWNLOAD_TOKEN")) {
            maven {
                url 'https://api.mapbox.com/downloads/v2/releases/maven'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    // Do not change the username below.
                    // This should always be `mapbox` (not your username).
                    username = "mapbox"
                    // Use the secret token you stored in gradle.properties as the password
                    password = keystoreProperties["MAPBOX_DOWNLOAD_TOKEN"]
                }
            }
        }
        google()
        mavenCentral()
    }

    android {
        namespace 'org.ramani.compose'
        compileSdkVersion 33
        buildToolsVersion "30.0.3"

        defaultConfig {
            minSdkVersion 25
            targetSdkVersion 33

            group = 'org.ramani-maps'
            versionCode 3
            version "0.1.0"
        }

        buildTypes {
            release {
                minifyEnabled false
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }
        kotlinOptions {
            jvmTarget = '17'
        }
        buildFeatures {
            compose true
        }
        composeOptions {
            kotlinCompilerExtensionVersion "1.4.7"
        }

        testOptions {
            unitTests.all {
                testLogging {
                    outputs.upToDateWhen { false }
                    events "passed", "failed", "skipped", "standardError"
                    showCauses true
                    showExceptions true
                }
            }
        }
    }

    dependencies {
        implementation "androidx.compose.foundation:foundation:$compose_ui_version"
        implementation "androidx.compose.material:material:$compose_ui_version"
        implementation "androidx.compose.ui:ui:$compose_ui_version"
        implementation 'androidx.core:core-ktx:1.10.1'
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

        api 'com.mapbox.maps:android:10.14.1'

        testImplementation 'junit:junit:4.13.2'
    }

    if (keystoreProperties.containsKey("ossrhUsername") && keystoreProperties.containsKey("ossrhPassword")) {
        afterEvaluate {
            publishing {
                publications {
                    release(MavenPublication) {
                        from components.release

                        pom {
                            name = 'Ramani-Mapbox'
                            packaging = 'aar'
                            description = 'An Android Compose library to manipulate Mapbox maps.'
                            url = 'https://github.com/ramani-maps/ramani-maps'

                            scm {
                                connection = 'scm:git:https://github.com/ramani-maps/ramani-maps'
                                developerConnection = 'scm:git:https://github.com/ramani-maps/ramani-maps'
                                url = 'https://github.com/ramani-maps/ramani-maps'
                            }

                            licenses {
                                license {
                                    name = 'Mozilla Public License 2.0'
                                    url = 'https://spdx.org/licenses/MPL-2.0.html'
                                }
                            }

                            developers {
                                developer {
                                    id = 'romanbapst'
                                    name = 'Roman Bapst'
                                    email = 'bapstroman@gmail.com'
                                }
                                developer {
                                    id = 'jonasvautherin'
                                    name = 'Jonas Vautherin'
                                    email = 'jonas.vautherin@gmail.com'
                                }
                            }
                        }
                    }
                }
                repositories {
                    maven {
                        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                        credentials {
                            username = keystoreProperties["ossrhUsername"]
                            password = keystoreProperties["ossrhPassword"]
                        }
                    }
                }
            }

            signing {
                useGpgCmd()
                sign publishing.publications.release
            }
        }
    }
}

